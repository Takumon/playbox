buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'  // Asciidoctor本体
        classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.11' // AsciidoctorでPDFを出力するためのプラグイン
        classpath 'org.asciidoctor:asciidoctorj-diagram:1.3.1'  // AsciidoctorでPlantUMLを使用可能にするプラグイン
    }
}

plugins {
    id 'org.kordamp.gradle.livereload' version '0.2.1'
}

apply plugin: 'eclipse'
apply plugin: 'org.asciidoctor.convert'
apply plugin: 'org.asciidoctor.gradle.asciidoctor'
apply plugin: 'org.kordamp.gradle.livereload' // ライブリロード用プラグイン


asciidoctorj {
    version = '1.5.4'
}


asciidoctor {
    outputDir = file("build/docs/asciidoc")  // 出力先ディレクトリ
    backends = ['html5','pdf'] // 出力ファイルの形式
    // adocファイル格納先
    sourceDir 'src/docs'

    resources {
        into "."

        from ('src/docs') {
            include(
                '/images/**', // 画像ファイル
                '/js/**',  // JSファイル
                '/css/**'  // CSSファイル
            )

        }
    }

    // ドキュメント属性
    attributes (
        //'stylesdir' : "$rootDir/src/theme/html5",    // HTML用テーマ格納フォルダ
        //'stylesheet' : 'asciidoctor.css',          // HTML用テーマ（お好みで変更してください）
        'pdf-fontsdir' : "$rootDir/src/fonts",      // PDF用フォント格納フォルダ
        'pdf-stylesdir' : "$rootDir/src/theme/pdf", // PDF用テーマ格納フォルダ
        'pdf-style' : 'default-theme.yml'            // PDF用テーマ
    )

    // 後処理
    doLast {
        // テーマ用画像資産を出力先のにコピー
        copy {
            from "$rootDir/src/theme/html5/images"
            into "$outputDir/images"
        }

        // 出力先の資産をGithub Pagesの公開フォルダにコピー
        delete 'docs'
        copy {
            from outputDir
            into "docs"
        }
        copy {
           from "src/githubpages/index.html"
           into "docs"
        }
    }
}

// ライブリロードの設定
liveReload {
    // build/docs/asciidocフォルダをルートフォルダとする
    docRoot asciidoctor.outputDir.canonicalPath
}

// GradleWrapper生成用タスク
task wrapper (type: Wrapper) {
    gradleVersion = '3.5'
}
